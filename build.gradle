plugins {
	id "architectury-plugin" version "3.4-SNAPSHOT"
	id "dev.architectury.loom" version "1.10-SNAPSHOT" apply false
}

architectury {
	minecraft = rootProject.minecraft_version
}

subprojects {
	apply plugin: "dev.architectury.loom"

	loom {
		silentMojangMappingsLicense()

		runs {
			client {
				vmArgs "-Xmx4G"
				programArgs "--width", "1920", "--height", "1080"
			}
		}
	}

	dependencies {
		minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
		mappings loom.officialMojangMappings()
	}
}

allprojects {
	apply plugin: "java"
	apply plugin: "architectury-plugin"
	apply plugin: "maven-publish"

	base {
		archivesName = rootProject.archives_base_name
	}

	version = mod_version
	group = project.maven_group

	compileJava {
		sourceCompatibility = 21
		targetCompatibility = 21
		options.encoding = "UTF-8"
	}

	repositories {
		mavenLocal()
		maven {
			url "https://maven.architectury.dev/"
			content {
				includeGroup "dev.architectury"
			}
		}
		maven {
			url "https://maven.ftb.dev/releases"
			content {
				includeGroup "dev.ftb.mods"
			}
		}
		maven {
			url "https://maven.ftb.dev/snapshots"
			content {
				includeGroup "dev.ftb.mods"
			}
		}
		maven {
			url = 'https://repo.spongepowered.org/repository/maven-public/'
			content {
				includeGroup 'org.spongepowered'
			}
		}
		maven {
			url = 'https://maven.parchmentmc.org/'
			content {
				includeGroup 'org.parchmentmc.data'
			}
		}
		maven {
			url = 'https://api.modrinth.com/maven/'
			content {
				includeGroup 'maven.modrinth'
			}
		}
	}

	java {
		withSourcesJar()
	}

}
